= Writing Custom Gradle Tasks

https://docs.gradle.org/current/userguide/tutorial_using_tasks.html[Tasks] are the cornerstone of getting things done in Gradle. They represent single atomic pieces of work within a build such as creating a JAR or linking an executable. Even though the Gradle distribution ships with a good range of {language-reference}/org.gradle.api.Task.html[predefined tasks and task types] and  most https://plugins.gradle.org[third-party plugins] providing additional tasks, it might still not be enough for your own project or organizational context.

Gradle's flexibility allows for the creation of custom tasks within a build script.

== What you'll create

This guide will walk you through the creation of a ad-hoc Gradle task and converting it to a custom task type.

== What youâ€™ll need

* About 20 minutes
* A text editor or IDE
* A Java Development Kit (JDK), version 1.7 or better
* A {user-manual}/installation.html#sec:download[Gradle distribution], version {gradle-version} or better

== Create an ad-hoc task

In a new folder create a `build.gradle` and enter the following

[source,groovy]
----
task hello { // <1>
  doLast { // <2>
      println 'Hello, World'
  }
}
----
<1> An ad-hoc task is created by simply using the keyword `task` followed by the
  name of the task and finally the closure that will configure the task.
<2> Ad-hoc task actions are added using {javadoc}/org/gradle/api/Task.html#doLast(groovy.lang.Closure)[doLast] or {javadoc}/org/gradle/api/Task.html#doFirst(groovy.lang.Closure)[doFirst].

Save the file and on the command-line type `gradle tasks`. Your new task will appear under `Other tasks`.

.Verifying that your task has been created
[listing]
----
Other tasks
-----------
hello
----

Run your task by executing `gradle hello` and it will produce a simple output

.Executing your ad-hoc task
[listing]
----
:hello // <1>
Hello, World // <2>
----
<1> Indicates that your `hello` task has executed.
<2> The output from your ad-hoc task.

Congratulations. You have you added your first ad-hoc task.

.Do not use << as an alternative to doLast
[IMPORTANT]
====
[source,groovy]
----
task hello << {
  println 'Hello, World'
}
----

You may comes across an alternative form for creating ad-hoc tasks in some older documentation and blog posts whereby the left-shift operator (`<<`) is used.

This form has caused too much confusion in the past especially among people that are new to Gradle. It also reduces readability. We have https://docs.gradle.org/3.2/release-notes#the-left-shift-operator-on-the-task-interface[deprecated this behaviour] and the functionality will be removed in Gradle 5.0.
====

== Add a task description

Although you have tested your new ad-hoc task and know that it works, it is good practice to tell others who will use your build script what the purpose of your new task is. It is also useful to categorise your task.

When you ran `gradle tasks` earlier you would have seen other tasks in the listing which contain descriptions and are grouped according to function.

.Well-documented tasks
[listing]
----
Build Setup tasks
-----------------
init - Initializes a new Gradle build. [incubating]
wrapper - Generates Gradle wrapper files. [incubating]
----

This is achieved by setting the `group` and `description` properties on the task.  Edit your ad-hoc task and add the following:

[source,groovy]
----
task hello {
  group 'Welcome'
  description 'Produces a greeting'
  doLast {
      println 'Hello, World'
  }
}
----

Run `gradle tasks` again and now you should see

[listing]
----
Welcome tasks
-------------
hello - Produces a greeting
----

Congratulations! You have created your ad-hoc task and documented it.

== Convert to a custom task type

Ad-hoc tasks are good for bespoke, small & simple tasks, but they do not scale. The logic cannot be re-used very easily.  You could just copy and paste and create more of the same, just changing the name and modifying the action slightly. This will soon become a maintainability trap and the build script will become unreadable.

This is the point where you need to convert your ad-hoc task into a custom task type, and this is achieved by creating a class inline within the build script.

Return to your `build.gradle` file and add the following class.

[source,groovy]
----
class Greeting extends DefaultTask { // <1> <2>
    String message // <3>
    String recipient

    @TaskAction // <4>
    void sayGreeting() {
        println "${message}, ${recipient}!" // <5>
    }
}
----
<1> As the build DSL in a `build.gradle` file is a Groovy-base DSL, the class will be a Groovy class.
<2> Although other task classes from the Gradle API can be used in specific circumstances, extending {javadoc}/org/gradle/api/DefaultTask.html[DefaultTask] is the most common scenario.
<3> Adding `message` and `recipient` properties allows instance of this custom task type to be configurable
<4> It is standard practice to have a default action for a task and this is achieved by adding the `@TaskAction` annotation to a *single* method in the task class. When the task is executed this is the method that will be called.
<5> Print the message using a standard Groovy interpolated string.

Now that you have created the class, what remains is to refactor the existing `hello` task:

[source,groovy]
----
task hello ( type : Greeting ) { // <1>
    group 'Welcome'
    description 'Produces a world greeting'
    message 'Hello' // <2>
    recipient 'World'
}
----
<1> Specify the task type by referencing the class type `Greeting` you have added above.
<2> Configure the message and the recipient.

Test your modification by running `gradle hello` again as you should see the same output

.Output after conversion to a custom task type
[listing]
----
:hello
Hello, World
----

Now that you have the custom task type, you can add additional tasks. Add a German version of the greeting by just creating an additional task.

.Adding a second task
[source,groovy]
----
task germanHello( type : Greeting ) {
    group 'Welcome'
    description 'Produces a German greeting'
    message 'Guten Tag'
    recipient 'Welt'
}
----

NOTE: No assignment is required during configuration as Gradle decorates the properties to allow for a more declarative DSL.

Run `gradle tasks` again to verify that the new task has been added.

.Output of 'gradle tasks' after adding second task.
[listing]
----
Welcome tasks
-------------
hello - Produces a greeting
germanHello - Produces a German greeting
----

Finally, run the new task by doing `gradle germanHello`

.Output of your second task.
[listing]
----
:germanHello
Guten Tag, Welt
----



////
[source,groovy]
----
class Greeting extends DefaultTask {

    String getMessage() { return message }
    void setMessage(String message) { this.message = message }

    String getRecipient() { return recipient }
    void setRecipient(String recipient) { this.recipient = recipient }

    @TaskAction
    void sayGreeting() {
      println "${message}, ${recipient}!"
    }

    private String message
    private String recipient
}
----
////

== Summary

That's it! You've worked through the steps necessary to create a custom Gradle Task. You should now have learnt how to

* Create an ad-hoc task and add an action using `doLast`.
* Document an ad-hoc task.
* Convert an ad-hoc task to a custom Gradle task type and creating task instances.
* Using `@TaskAction` to set a default action for a task type.

== Next steps

// TODO: This should point to a GS guide on organizing build logic
* Having classes in a build script will soon lead to a messy and potentially unmaintainable build script. Learn how to {user-manual}/organizing_build_logic.html[organize your build logic].

== Help improve this guide

Have feedback or a question? Found a typo? Like all Gradle guides, help is just a GitHub Issue away. Please add an issue or pull request to the https://github.com/{repo-path}/[{repo-path}] and we'll get back to you.
