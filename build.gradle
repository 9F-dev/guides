plugins {
    id "org.ajoberstar.github-pages" version "1.4.2"
    id "org.asciidoctor.convert" version "1.5.3"
}
apply plugin: "base"

asciidoctor {
    def gradleVersion = '3.0'
    sourceDir = rootDir
    sources {
      include "README.adoc"
    }
    outputDir = buildDir
    backends = ["html5"]
    attributes "source-highlighter": "coderay",
            "coderay-linenums-mode": "table",
            imagesdir: "images",
            icons: "font",
            sectanchors: true,
            sectlinks: true,
            linkattrs: true,
            encoding: "utf-8",
            idprefix: "",
            toc: "right",
            toclevels: 1,
            "gradle-version": gradleVersion,
            "user-manual": "https://docs.gradle.org/$gradleVersion/userguide/",
            "language-reference": "https://docs.gradle.org/$gradleVersion/dsl/"
}

task copyIndex {
    doLast {
        file("${asciidoctor.outputDir}/${asciidoctor.backends[0]}/README.html")
            .renameTo("${asciidoctor.outputDir}/${asciidoctor.backends[0]}/index.html")
    }
}

githubPages {
    repoUri = "git@github.com:gradle-guides/gs-building-jvm-libraries.git"
    pages {
        from "${asciidoctor.outputDir}/${asciidoctor.backends[0]}"
    }
}

assemble.dependsOn "asciidoctor", "copyIndex"
