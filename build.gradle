plugins {
    id 'com.gradle.build-scan' version '1.8'
    id 'groovy'
    id 'com.github.hierynomus.license' version '0.13.1'
    id 'com.gradle.plugin-publish' version '0.9.7'
    id 'org.ysb33r.cloudci' version '1.0'
    id 'org.ysb33r.gradletest' version '1.0-beta11'
}

buildScan {
    licenseAgreementUrl = 'https://gradle.com/terms-of-service'
    licenseAgree = 'yes'
}

group = 'org.gradle.guides'
version = '0.6.4'

repositories {
    jcenter()

    maven {
        url "https://plugins.gradle.org/m2/"
    }
}

ext {
    spockGroovyVer = GroovySystem.version.replaceAll(/\.\d+$/,'')
}

dependencies {
    compile localGroovy()
    compile gradleApi()

    compile 'net.sourceforge.nekohtml:nekohtml:1.9.21'
    compile 'org.codehaus.groovy.modules:http-builder-ng-core:0.11.1'

    compile 'org.asciidoctor:asciidoctor-gradle-plugin:1.5.3'
    compile 'org.ajoberstar:gradle-git-publish:0.2.1'

    runtime 'org.ysb33r.gradle:gradle-cloudci-plugin:1.0'
    runtime 'org.ysb33r.gradle:gradletest:1.0-beta11'

    testImplementation "org.spockframework:spock-core:1.0-groovy-${spockGroovyVer}", {
        exclude module : 'groovy-all'
    }
}

license {
    header = rootProject.file('config/HEADER')
    strictCheck = true
    ignoreFailures = false
    mapping {
        groovy ='DOUBLESLASH_STYLE'
    }
    ext.year = '2017'
    excludes([
            '**/*.adoc', '**/*.md',
            '**/*.properties',
            '**/*CompatibilitySpec.groovy'
    ])
}

pluginBundle {
    website = 'https://guides.gradle.org'
    vcsUrl = 'https://github.com/gradle-guides/gradle-guides-plugin'
    tags = ['gradle', 'guides', 'documentation']
    version = project.version

    plugins {
        basePlugin {
            id = 'org.gradle.guides.base'
            displayName = 'Gradle Guides Base Plugin'
            description = 'Base plugin required by mew Gradle guide plugins'
        }
        gsPlugin {
            id = 'org.gradle.guides.getting-started'
            displayName = 'Gradle Guides Getting Started Plugin'
            description = 'Plugin required for authoring new Gradle Getting Started guides'
        }
        topicalPlugin {
            id = 'org.gradle.guides.topical'
            displayName = 'Gradle Guides Topical Plugin'
            description = 'Plugin required for authoring new Gradle Topical guides'
        }
        tutorialPlugin {
            id = 'org.gradle.guides.tutorial'
            displayName = 'Gradle Guides Topical Plugin'
            description = 'Plugin required for authoring new Gradle Tutorial guides'
        }
        jvmtestPlugin {
            id = 'org.gradle.guides.test-jvm-code'
            displayName = 'Gradle Guides JVM Code Snippet Test Plugin'
            description = 'Plugin required for testing of JVM code snippets used in guides'
        }
    }
}

gradleTest {
  versions '3.5', '4.0'
}

publishPlugins {
    enabled = false
    dependsOn build

    onlyIf { !version.endsWith('-SNAPSHOT')}
}

travisci  {
    publishPlugins {
        enabled = System.getenv('TRAVIS_BRANCH') == 'master' && System.getenv('TRAVIS_PULL_REQUEST') == 'false'
    }

    task release {
        dependsOn build, publishPlugins
    }
}
