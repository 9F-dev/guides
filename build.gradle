plugins {
    id "com.gradle.build-scan" version "1.11"
    id "org.gradle.guides.getting-started" version "0.11.2"
    id "org.gradle.guides.test-jvm-code" version "0.11.2"
}

guide {
    repoPath "gradle-guides/building-kotlin-jvm-libraries"
    mainAuthor "Eric Wendelin"
}

asciidoctor {
    attributes "source-highlighter": "highlightjs",
        stylesheet: null
    extensions {
        postprocessor { document, output ->
            if (document.basebackend("html")) {
                def kotlinHighlightJs = """<script src="https://cdnjs.cloudflare.com/ajax/libs/highlight.js/9.12.0/languages/kotlin.min.js"></script>"""
                output.replaceAll(~/\/(8.9.1)\/highlight\.min\.js"><\/script>/, """/9.12.0/highlight.min.js"></script>${kotlinHighlightJs}""")
            } else {
                output
            }
        }
    }
}

buildScan {
    licenseAgreementUrl = "https://gradle.com/terms-of-service"
    licenseAgree = "yes"
    if (System.getenv("CI")) {
        publishAlways()
        tag "CI"
    }
}
