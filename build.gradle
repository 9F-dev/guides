buildscript {
    dependencies {
        classpath "org.jsoup:jsoup:1.10.1"
    }
}

plugins {
    id "org.ajoberstar.github-pages" version "1.4.2"
    id "org.asciidoctor.convert" version "1.5.3"
}
apply plugin: "base"

def repoPath = 'gradle-guides/gs-building-jvm-libraries'

asciidoctor {
    def gradleVersion = '3.0'
    sourceDir = rootDir
    sources {
      include "README.adoc"
    }
    outputDir = buildDir
    backends = ["html5"]
    attributes "source-highlighter": "coderay",
            "coderay-linenums-mode": "table",
            imagesdir: "src/images",
            stylesheet: "asciidoctor.css",
            linkcss: true,
            docinfodir: "src/asciidoc",
            docinfo1: "",
            icons: "font",
            sectanchors: true,
            sectlinks: true,
            linkattrs: true,
            encoding: "utf-8",
            idprefix: "",
            toc: "right",
            toclevels: 1,
            "repo-path": repoPath,
            "gradle-version": gradleVersion,
            "user-manual": "https://docs.gradle.org/$gradleVersion/userguide/",
            "language-reference": "https://docs.gradle.org/$gradleVersion/dsl/"
    extensions {
        postprocessor { document, output ->
            if (!document.basebackend("html")) {
                throw new IllegalArgumentException("Expected html Asciidoctor backend")
            }
            def doc = org.jsoup.Jsoup.parse(output, "UTF-8")
            def header = doc.getElementById("header")
            def logoFile = file("src/images/gradle-guides.svg")
            def logoSvg = logoFile.text
            header.prepend("""<div style="padding-top: 10px;"><a href="https://guides.gradle.org">$logoSvg</a></div>""")
            doc.html()
        }
    }
}

task copyIndex {
    doLast {
        file("${asciidoctor.outputDir}/${asciidoctor.backends[0]}/README.html")
            .renameTo("${asciidoctor.outputDir}/${asciidoctor.backends[0]}/index.html")
    }
}

task copyIcon(type: Copy) {
    from "icon/"
    into "${asciidoctor.outputDir}/${asciidoctor.backends[0]}/icon"
}

githubPages {
    def ghToken = System.getenv('GH_TOKEN')
    if (ghToken) {
        repoUri = "https://github.com/${repoPath}.git"
        credentials {
            username = ghToken
            password = '\n'
        }
    } else {
        repoUri = "git@github.com:${repoPath}.git"
    }
    pages {
        from "${asciidoctor.outputDir}/${asciidoctor.backends[0]}"
    }
    commitMessage = 'Publish to GitHub Pages'
}

assemble.dependsOn "asciidoctor", "copyIndex", "copyIcon"
