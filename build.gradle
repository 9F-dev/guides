plugins {
    id 'com.gradle.build-scan' version '1.13.4'
    id 'groovy'
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version '0.10.0'
    id 'org.ysb33r.cloudci' version '1.0'
    id 'org.ysb33r.gradletest' version '1.0-beta12'
    id 'maven-publish'
}

apply from: "$rootDir/gradle/functional-test.gradle"

buildScan {
    licenseAgreementUrl = 'https://gradle.com/terms-of-service'
    licenseAgree = 'yes'
    if (System.getenv("CI")) {
        publishAlways()
        tag("CI")
    }
}

group = 'org.gradle.guides'
version = '0.13.4-SNAPSHOT'

repositories {
    jcenter()
    gradlePluginPortal()
}

ext {
    spockGroovyVer = GroovySystem.version.replaceAll(/\.\d+$/,'')
}

dependencies {
    compile 'net.sourceforge.nekohtml:nekohtml:1.9.21'
    compile 'org.codehaus.groovy.modules:http-builder-ng-core:0.11.1'

    compile 'org.asciidoctor:asciidoctor-gradle-plugin:1.5.3'
    compile 'org.ajoberstar:gradle-git-publish:0.2.1'

    runtime 'org.ysb33r.gradle:gradle-cloudci-plugin:1.0'
    runtime 'org.ysb33r.gradle:gradletest:1.0-beta11'

    testImplementation "org.spockframework:spock-core:1.0-groovy-${spockGroovyVer}", {
        exclude module : 'groovy-all'
    }
}

pluginBundle {
    website = 'https://guides.gradle.org'
    vcsUrl = 'https://github.com/gradle-guides/gradle-guides-plugin'
    tags = ['gradle', 'guides', 'documentation']
}

gradlePlugin {
    plugins {
        basePlugin {
            id = 'org.gradle.guides.base'
            implementationClass = 'org.gradle.guides.BasePlugin'
            displayName = 'Gradle Guides Base Plugin'
            description = 'Base plugin required by new Gradle guide plugins'
        }
        gsPlugin {
            id = 'org.gradle.guides.getting-started'
            implementationClass = 'org.gradle.guides.GettingStartedPlugin'
            displayName = 'Gradle Guides Getting Started Plugin'
            description = 'Plugin required for authoring new Gradle Getting Started guides'
        }
        topicalPlugin {
            id = 'org.gradle.guides.topical'
            implementationClass = 'org.gradle.guides.TopicalPlugin'
            displayName = 'Gradle Guides Topical Plugin'
            description = 'Plugin required for authoring new Gradle Topical guides'
        }
        tutorialPlugin {
            id = 'org.gradle.guides.tutorial'
            implementationClass = 'org.gradle.guides.TutorialPlugin'
            displayName = 'Gradle Guides Topical Plugin'
            description = 'Plugin required for authoring new Gradle Tutorial guides'
        }
        jvmtestPlugin {
            id = 'org.gradle.guides.test-jvm-code'
            implementationClass = 'org.gradle.guides.TestJvmCodePlugin'
            displayName = 'Gradle Guides JVM Code Snippet Test Plugin'
            description = 'Plugin required for testing of JVM code snippets used in guides'
        }
    }
}

gradleTest {
    versions '3.5', '4.4.1'

    // Should be declared by gradleTest plugin.
    inputs.files tasks.jar.outputs.files
}

publishPlugins {
    enabled = false
    dependsOn build

    onlyIf { !version.endsWith('-SNAPSHOT')}
}

travisci  {
    publishPlugins {
        enabled = System.getenv('TRAVIS_BRANCH') == 'master' && System.getenv('TRAVIS_PULL_REQUEST') == 'false'
    }

    task release {
        dependsOn build, publishPlugins
    }
}
