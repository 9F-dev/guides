apply plugin : 'groovy'

ext {
    spockGroovyVer = GroovySystem.version.replaceAll(/\.\d+$/,'')
}

dependencies {
    testCompile localGroovy()
    testCompile ("org.spockframework:spock-core:1.0-groovy-${spockGroovyVer}") {
        exclude module : 'groovy-all'
    }
    testCompile 'commons-io:commons-io:2.5'
    testCompile gradleTestKit()
}




repositories {
    maven {
        url "https://plugins.gradle.org/m2/"
    }
}

configurations {
    pluginClasspath
}

dependencies {
    pluginClasspath 'com.gradle.publish:plugin-publish-plugin:0.9.7'
}

test {
    ext {
        httpServerPort = 65000
    }
    systemProperties 'gradle.portal.url' : "http://localhost:${httpServerPort}"
    systemProperties 'HTTPSERVER_PORT' : httpServerPort
    systemProperties 'TEMPLATE_DIR' : file('src/publicationTest').absolutePath
    systemProperties 'PLUGIN_CLASSPATH' : file("${buildDir}/pluginClasspath").absolutePath

    inputs.dir 'src/publicationTest'

    doFirst {
        copy {
            from configurations.pluginClasspath
            into "${buildDir}/pluginClasspath"
        }
    }
}




