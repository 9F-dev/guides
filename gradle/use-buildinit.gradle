buildscript {
    dependencies {
        classpath gradleTestKit()
    }
}

import org.gradle.testkit.runner.GradleRunner
import org.apache.tools.ant.filters.*

task runBuildInit {

    ext {
        buildType       = 'java-library'
        destDir         = file("${buildDir}/init/${project.name}")
        outputLogFileDir = file("${buildDir}/init/output")
        tmpOutputDir     = file("${buildDir}/tmp/init/output")
        initOutputFile   = file("${outputLogFileDir}/init.txt")
        buildOutputFile  = file("${outputLogFileDir}/build.txt")
        docOutputFile    = file("${outputLogFileDir}/javadoc.txt")
        manifestFile     = file("${outputLogFileDir}/META-INF/MANIFEST.MF")
    }

    outputs.dir { destDir }
    outputs.file { initOutputFile }
    outputs.file { buildOutputFile }
    outputs.file { docOutputFile }

    inputs.property 'gradleVer' , gradle.gradleVersion

    doLast {
        File tmpDir = file("${buildDir}/tmp/init/${project.name}")
        File tmpInitFile  = new File(tmpOutputDir,initOutputFile.name)
        File tmpBuildFile = new File(tmpOutputDir,buildOutputFile.name)
        File tmpDocFile   = new File(tmpOutputDir,docOutputFile.name)

        destDir.deleteDir()
        tmpDir.deleteDir()
        mkdir destDir
        mkdir tmpDir
        mkdir initOutputFile.parentFile
        mkdir tmpBuildFile.parentFile

        tmpInitFile.withWriter { Writer output ->
            GradleRunner.create().forwardStdOutput(output).withProjectDir(tmpDir).
                withArguments('init','--type',buildType).build()
        }

        tmpBuildFile.withWriter { Writer output ->
            GradleRunner.create().forwardStdOutput(output).withProjectDir(tmpDir).
                withArguments('build').build()
        }

        File editLibrary = new File(tmpDir,'src/main/java/Library.java')
        editLibrary.text = file('contents/javadoc-prefix.txt').text + editLibrary.text

        tmpDocFile.withWriter { Writer output ->
            GradleRunner.create().forwardStdOutput(output).withProjectDir(tmpDir).
                withArguments('javadoc').build()
        }

        copy {
            from tmpDir
            into destDir

            exclude '.gradle/**'
            exclude 'build/**'

            filesMatching (['build.gradle','**/*.java']) {
                filter { line ->
                    if (line =~ /^(\/\*)|(\s*\*)|(\/\/)/) {
                        null
                    } else {
                        line
                    }
                }
            }

            filesMatching (['settings.gradle']) {
                filter { line->
                    if(line=~/^rootProject/) {
                        "${line} // <1>"
                    } else {
                        null
                    }
                }
            }

            filesMatching (['build.gradle']) {
                filter { line ->
                    if (line=~/\s*jcenter/) {
                        "${line} // <1>"
                    } else if (line=~/\s*api.+commons-math/) {
                        "${line} // <2>"
                    } else if (line=~/\s*implementation.+guava/) {
                        "${line} // <3>"
                    } else if (line=~/\s*testImplementation.+junit/) {
                        "${line} // <4>"
                    } else {
                        line
                    }
                }
            }

            filesMatching( ['src/main/java/Library.java'] ) {
                filter ConcatFilter, prepend: file('contents/javadoc-prefix.txt'),
                    append: file('contents/javadoc-postfix.txt')
            }
        }

        copy {
            from tmpOutputDir
            into outputLogFileDir

            filter { line ->
                if( line=~ /^Download|Total time/) {
                    null
                } else {
                    line
                }
            }
        }

        File editBuildFile = new File(tmpDir,'build.gradle')
        File jarConfig = project.file("contents/jar-postfix.txt")
        editBuildFile.withWriterAppend { Writer writer ->
            writer.println jarConfig.text
        }

        GradleRunner.create().withProjectDir(tmpDir).
            withArguments('jar').build()

        copy {
            from( zipTree(new File(tmpDir,'build/libs/building-java-libraries-0.1.0.jar')) ) {
                include 'META-INF/MANIFEST.MF'
            }
            into outputLogFileDir
        }
    }
}
