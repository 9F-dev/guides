plugins {
    id 'java-library'
}

dependencies {
    implementation 'com.google.guava:guava:22.0'

    testImplementation 'junit:junit:4.12'
}

// tag::compileJava[]
ext.moduleName = 'org.gradle.actors' // <1>

compileJava {
    doFirst {
        classpath = files()  // <2>
        options.compilerArgs = [
            '--module-path', configurations.compileClasspath.asPath,
            '--source-path', files(sourceSets.main.java.srcDirs).asPath,
            '-d', "${sourceSets.main.java.outputDir}/$moduleName"
        ]
    }
    doLast {
        copy {
            from "${sourceSets.main.java.outputDir}/$moduleName"
            into sourceSets.main.java.outputDir
        }
    }
}
// end::compileJava[]

// tag::compileTestJava[]
compileTestJava {
    doFirst {
        classpath = files()
        options.compilerArgs = [
            '--module-path', files(sourceSets.main.java.outputDir, configurations.testCompileClasspath).asPath, // <1>
            '--add-modules', "junit",  // <2>
            '--add-reads', "$moduleName=junit", // <3>
            '--patch-module', "$moduleName=" + files(sourceSets.test.java.srcDirs).asPath, // <4>
        ]
    }
}
// end::compileTestJava[]

// tag::jar[]
jar {
    exclude moduleName
}
// end::jar[]

// tag::test[]
test {
    doFirst {
        classpath = files()
        jvmArgs = [
            '--module-path', files(sourceSets.main.java.outputDir, configurations.testRuntimeClasspath).asPath, // <1>
            '--add-modules', 'ALL-MODULE-PATH', // <2>
            '--add-reads', "$moduleName=junit", // <3>
            '--patch-module', "$moduleName=" + files(sourceSets.test.java.outputDir).asPath, // <4>
        ]
    }
}
// end::test[]
