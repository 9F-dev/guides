plugins {
    id 'java-library'
}

dependencies {
    implementation 'com.google.guava:guava:22.0'

    testImplementation 'junit:junit:4.12'
}

// tag::compileJava[]
ext.moduleName = 'org.gradle.actors' // <1>

compileJava {
    doFirst {
        options.compilerArgs = [
            '--module-path', classpath.asPath,
            '--source-path', files(sourceSets.main.java.srcDirs).asPath,
            '-d', "$destinationDir/$moduleName"
        ]
        classpath = files()  // <2>
    }
    doLast {
        copy {
            from "$destinationDir/$moduleName"
            into destinationDir
        }
    }
}
// end::compileJava[]

// tag::compileTestJava[]
compileTestJava {
    doFirst {
        options.compilerArgs = [
            '--module-path', classpath.asPath, // <1>
            '--add-modules', 'junit',  // <2>
            '--add-reads', "$moduleName=junit", // <3>
            '--patch-module', "$moduleName=" + files(sourceSets.test.java.srcDirs).asPath, // <4>
        ]
        classpath = files()
    }
}
// end::compileTestJava[]

// tag::jar[]
jar {
    exclude moduleName
}
// end::jar[]

// tag::test[]
test {
    doFirst {
        jvmArgs = [
            '--module-path', classpath.asPath, // <1>
            '--add-modules', 'ALL-MODULE-PATH', // <2>
            '--add-reads', "$moduleName=junit", // <3>
            '--patch-module', "$moduleName=" + files(sourceSets.test.java.outputDir).asPath, // <4>
        ]
        classpath = files()
    }
}
// end::test[]
