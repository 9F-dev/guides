subprojects {
    afterEvaluate {
        repositories {
            jcenter()
        }

        // tag::compileJava[]
        compileJava {
            doFirst {
                classpath = files()
                options.compilerArgs = [
                    '--module-path', configurations.compileClasspath.asPath,
                    '--source-path', files(sourceSets.main.java.srcDirs).asPath,
                    '-d', "${sourceSets.main.java.outputDir}/$moduleName"
                ]
            }
        }
        // end::compileJava[]

        compileTestJava {
            doFirst {
                classpath = files()
                options.compilerArgs = [
                    '--module-path', files(sourceSets.main.java.outputDir, configurations.testCompileClasspath).asPath,
                    '--add-modules', "junit",
                    '--add-reads', "$moduleName=junit",
                    '--patch-module', "$moduleName=" + files(sourceSets.test.java.srcDirs).asPath,
                ]
            }
        }

        // tag::jar[]
        jar {
            exclude moduleName
            with copySpec {
                from file("${sourceSets.main.java.outputDir}/$moduleName")
            }
        }
        // end::jar[]

        test {
            doFirst {
                classpath = files()
                jvmArgs = [
                    '--module-path', files(sourceSets.main.java.outputDir, configurations.testRuntimeClasspath).asPath,
                    '--add-modules', 'ALL-MODULE-PATH',
                    '--add-reads', "$moduleName=junit",
                    '--patch-module', "$moduleName=" + files(sourceSets.test.java.outputDir).asPath,
                ]
            }
        }
    }
}
