plugins {
    id 'java'
    id 'application'
}

import java.util.regex.Matcher;

dependencies {
    implementation project(':tale')

    runtimeOnly project(':pigs')
    runtimeOnly project(':bears')
}

ext.moduleName = 'org.gradle.fairy.app'

// tag::run[]
mainClassName = "$moduleName/org.gradle.fairy.app.StoryTeller" // <1>

run {
    doFirst {
        classpath = files()
        jvmArgs = [
            '--module-path', files(sourceSets.main.java.outputDir, configurations.runtimeClasspath).asPath,
            '--module', mainClassName // <2>
        ]
    }
}
// end::run[]

// tag::startScripts[]
startScripts {
    doFirst {
        classpath = files()
        defaultJvmOpts = [
            '--module-path', 'APP_HOMElib',  // <1>
            '--module', mainClassName
        ]
    }
    doLast{
        def bashFile = new File(outputDir, applicationName)
        String bashContent = bashFile.text
        bashFile.text = bashContent.replaceFirst('APP_HOME', Matcher.quoteReplacement('$APP_HOME/'))

        def batFile = new File(outputDir, applicationName + ".bat")
        String batContent = batFile.text
        batFile.text = batContent.replaceFirst('APP_HOME', Matcher.quoteReplacement('%APP_HOME%\\'))
    }
}
// end::startScripts[]
