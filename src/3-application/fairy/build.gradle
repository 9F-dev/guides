plugins {
    id 'java'
    id 'application'
}

import java.util.regex.Matcher;

dependencies {
    implementation project(':tale')

    runtimeOnly project(':pigs')
    runtimeOnly project(':bears')
}

ext.moduleName = 'org.gradle.fairy.app'

mainClassName = "$moduleName/org.gradle.fairy.app.StoryTeller"

run {
    doFirst {
        classpath = files()
        jvmArgs = [
            '--module-path', files(sourceSets.main.java.outputDir, configurations.runtimeClasspath).asPath,
            '--module', mainClassName
        ]
    }
}

startScripts {
    doFirst {
        classpath = files()
        defaultJvmOpts = [
            '--module-path', 'DOLLARSIGNAPP_HOME/lib',
            '--module', mainClassName
        ]
    }
    doLast{
        def bashFile = new File(outputDir, applicationName)
        String bashContent = bashFile.text
        bashFile.text = bashContent.replaceFirst('DOLLARSIGN', Matcher.quoteReplacement('$'))

        def batFile = new File(outputDir, applicationName + ".bat")
        String batContent = batFile.text
        batFile.text = batContent.replaceFirst('DOLLARSIGNAPP_HOME', Matcher.quoteReplacement('%APP_HOME%'))
    }
}
