plugins {
    id 'java-library'
}

dependencies {
    implementation 'com.google.guava:guava:22.0'

    testImplementation 'junit:junit:4.12'
}

compileJava {
    doFirst {
        classpath = files()
        options.compilerArgs = [
            '--module-path', configurations.compileClasspath.asPath,
            '--source-path', files(sourceSets.main.java.srcDirs).asPath,
        ]
    }
}

compileTestJava {
    doFirst {
        classpath = files()
        options.compilerArgs = [
            '--patch-module', 'org.gradle.actors=' + files(sourceSets.test.java.srcDirs, sourceSets.main.java.outputDir).asPath,
            '--module-path', configurations.testCompileClasspath.asPath,
        ]
    }
}

test {
    exclude 'module-info*'
    doFirst {
        copy {
            from files(sourceSets.main.java.outputDir, sourceSets.test.java.outputDir)
            into 'build/mods/org.gradle.actors'
        }
        classpath = files('build/mods/org.gradle.actors')
        jvmArgs = [
            '--module-path', files('build/mods', configurations.testRuntimeClasspath).asPath,
        ]
        println jvmArgs
    }
}
