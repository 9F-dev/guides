plugins {
    id 'java-library'
}

dependencies {
    implementation 'com.google.guava:guava:22.0'

    testImplementation 'junit:junit:4.12'
}

ext.moduleName = 'org.gradle.actors'

compileJava {
    doFirst {
        classpath = files()
        options.compilerArgs = [
            '--module-path', configurations.compileClasspath.asPath,
            '--source-path', files(sourceSets.main.java.srcDirs).asPath,
        ]
    }
    doLast {
        // --module-path wants a directory, which is a directory of modules
        // Copying the files so as not to change how the jar file is created.
        copy {
            from sourceSets.main.java.outputDir
            into file("build/mods/$moduleName")
        }
    }
}

compileTestJava {
    doFirst {
        classpath = files()
        options.compilerArgs = [
            '--module-path', files('build/mods', configurations.testCompileClasspath).asPath,
            '--patch-module', "$moduleName=" + files(sourceSets.test.java.srcDirs).asPath,
        ]
    }
}

test {
    doFirst {
        classpath = files()
        jvmArgs = [
            '--module-path', files('build/mods', configurations.testCompileClasspath).asPath,
            '--patch-module', "$moduleName=" + files(sourceSets.test.java.outputDir).asPath,
            '--add-modules', 'ALL-MODULE-PATH'
        ]
    }
}
