plugins {
    id 'java-library'
}

dependencies {
    implementation 'com.google.guava:guava:22.0'

    testImplementation 'junit:junit:4.12'
}

ext.moduleName = 'org.gradle.actors'

compileJava {
    // --module-path wants a directory, which is a directory of modules
    destinationDir = file("${sourceSets.main.java.outputDir}/$moduleName")
    doFirst {
        classpath = files()
        options.compilerArgs = [
            '--module-path', configurations.compileClasspath.asPath,
            '--source-path', files(sourceSets.main.java.srcDirs).asPath,
        ]
    }
}

compileTestJava {
    doFirst {
        classpath = files()
        options.compilerArgs = [
            '--module-path', files(sourceSets.main.java.outputDir, configurations.testCompileClasspath).asPath,
            '--patch-module', "$moduleName=" + files(sourceSets.test.java.srcDirs).asPath,
        ]
    }
}

jar {
    exclude moduleName
    with copySpec {
        from file(compileJava.destinationDir)
    }
}

test {
    doFirst {
        classpath = files()
        jvmArgs = [
            '--module-path', files(sourceSets.main.java.outputDir, configurations.testCompileClasspath).asPath,
            '--patch-module', "$moduleName=" + files(sourceSets.test.java.outputDir).asPath,
            '--add-modules', 'ALL-MODULE-PATH'
        ]
    }
}
